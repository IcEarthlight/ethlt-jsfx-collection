desc: Disturation
author: Earthlight
version: 1.1

slider1:pregain=0<-12,12,0.1>Pre-Gain (dB)
slider2:dist=0<0,1,0.01>Distortion Amount
slider3:satr=0<0,1,0.01>Saturation Amount
slider4:sclp=0<-32,0,0.1:log=-6>Softclip Threshold (dB)

@init

pos_peak = 0;
neg_peak = 0;

last_pregain_fac = 2 ^ (pregain/6);
last_dist = dist;
last_satr = satr;
last_sclp_fac = 2 ^ (sclp/6);

@slider

pregain_fac = 2 ^ (pregain/6);
sclp_fac = 2 ^ (sclp/6);

lp_coef = (1/3) ^ (1000 / srate);

@block

d_pregain_fac = (pregain_fac - last_pregain_fac) / samplesblock;
d_dist = (dist - last_dist) / samplesblock;
d_satr = (satr - last_satr) / samplesblock;
d_sclp_fac = (sclp_fac - last_sclp_fac) / samplesblock;

@sample

last_pregain_fac += d_pregain_fac;
last_dist += d_dist;
last_satr += d_satr;
last_sclp_fac += d_sclp_fac;


spl0 *= last_pregain_fac;
spl1 *= last_pregain_fac;

pos_peak = max(pos_peak, max(spl0, spl1));
neg_peak = min(neg_peak, min(spl0, spl1));

sclp ? (
    abs(spl0) > last_sclp_fac ? spl0 = (
        spl0 > 0 ? (-exp(-(spl0 - last_sclp_fac) / (1 - last_sclp_fac)) + 1) * (1 - last_sclp_fac) + last_sclp_fac
                 : ( exp( (spl0 + last_sclp_fac) / (1 - last_sclp_fac)) - 1) * (1 - last_sclp_fac) - last_sclp_fac
    );
    abs(spl1) > last_sclp_fac ? spl1 = (
        spl1 > 0 ? (-exp(-(spl1 - last_sclp_fac) / (1 - last_sclp_fac)) + 1) * (1 - last_sclp_fac) + last_sclp_fac
                 : ( exp( (spl1 + last_sclp_fac) / (1 - last_sclp_fac)) - 1) * (1 - last_sclp_fac) - last_sclp_fac
    );
) : (
    spl0 = min(max(spl0, -1), 1);
    spl1 = min(max(spl1, -1), 1);
);

temp0 = 2 * last_dist / (1.01 - last_dist);
spl0 = (1 + temp0) * spl0 / (1 + temp0*abs(spl0));
spl1 = (1 + temp0) * spl1 / (1 + temp0*abs(spl1));

temp1 = sin(last_satr / 2 * $pi);
satr ? (
    spl0 = sin(spl0 * last_satr / 2 * $pi) / temp1;
    spl1 = sin(spl1 * last_satr / 2 * $pi) / temp1;
);

@gfx 432 240

gfxSize = min(gfx_w, gfx_h);

gfx_set(0.1, 0.1, 0.12, 1);
gfx_x = gfx_y = 0;
gfx_rectto(gfx_w, gfx_h);

gfx_set(1, 1, 1, 0.5);
gfx_x = gfx_w / 2; gfx_y = 0;
gfx_lineto(gfx_x, gfx_h, 0);
gfx_x = 0; gfx_y = gfx_h / 2;
gfx_lineto(gfx_w, gfx_y, 0);

gfx_set(1, 0.3, 0.1, 0.2);
gfx_x = (gfx_w + neg_peak * gfx_h) / 2; gfx_y = 0;
gfx_rectto((gfx_w + pos_peak * gfx_h) / 2, gfx_h);

gfx_h > 100 ? (
    gfx_set(1, 1, 1, 0.7);
    gfx_x = (gfx_w + gfxSize) / 2 - 20; gfx_y = gfx_h / 2;
    gfx_drawchar($'I');
    gfx_drawchar($'N');
    gfx_x = gfx_w/2 + 2; gfx_y = 0;
    gfx_drawchar($'O');
    gfx_drawchar($'U');
    gfx_drawchar($'T');
);

gfx_set(1, 1, 1, 0.8);
gfx_x = 0; gfx_y = gfxSize;
x_max = 1 / gfxSize * gfx_w;
x = -x_max;

while (
    y = x;
    sclp ? (
        abs(y) > sclp_fac ? y = (
            y > 0 ? (-exp(-(y - sclp_fac) / (1 - sclp_fac)) + 1) * (1 - sclp_fac) + sclp_fac
                  : ( exp( (y + sclp_fac) / (1 - sclp_fac)) - 1) * (1 - sclp_fac) - sclp_fac
        );
    ) : (
        y = min(max(y, -1), 1);
    );
    temp0_ = 2 * dist / (1.01 - dist);
    y = (1 + temp0_) * y / (1 + temp0_*abs(y));
    satr ? y = sin(y * satr / 2 * $pi) / sin(satr / 2 * $pi);
    x != -x_max ? gfx_lineto((gfx_w + x*gfx_h) / 2, (-y+1)/2 * gfx_h, 1)
                : ( gfx_x = (gfx_w + x*gfx_h) / 2; gfx_y = (-y+1)/2 * gfx_h; );
    (x += 0.01) < x_max;
);

gfx_set(0.2, 0.2, 0.24, 0.5);
gfx_x = 0; gfx_y = 0;
gfx_rectto((gfx_w - gfx_h) / 2, gfx_h);
gfx_x = (gfx_w + gfx_h) / 2; gfx_y = 0;
gfx_rectto(gfx_w, gfx_h);

gfx_set(1, 0.3, 0.1, 0.05);
neg_peak < -1 ? (
    gfx_x = (gfx_w + neg_peak * gfx_h) / 2; gfx_y = 0;
    gfx_rectto((gfx_w - gfx_h) / 2, gfx_h);
);
pos_peak > 1 ? (
    gfx_x = (gfx_w + gfx_h) / 2; gfx_y = 0;
    gfx_rectto((gfx_w + pos_peak * gfx_h) / 2, gfx_h);
);
pos_peak = neg_peak = 0;
