desc: MidiPump
author: Earthlight
version: 0.0

slider1:channel=3<-1,0,15,1{All,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Controller Channel
slider2:source=24<0,127,1{0 Bank Sel M,1 Mod Wheel M,2 Breath M,3,4 Foot P M,5 Porta M,6 Data Entry M,7 Vol M,8 Balance M,9,10 Pan M,11 Expression M,12 Ctrl 1 M,13 Ctrl 2 M,14,15,16 GP Slider 1,17 GP Slider 2,18 GP Slider 3,19 GP Slider 4,20,21,22,23,24,25,26,27,28,29,30,31,32 Bank Sel L,33 Mod Wheel L,34 Breath L,35,36 Foot P L,37 Porta L,38 Data Entry L,39 Vol L,40 Balance L,41,42 Pan L,43 Expression L,44 Ctrl 1 L,45 Ctrl 2 L,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64 Hold P sw,65 Porta sw,66 Sustenuto sw,67 Soft P sw,68 Legato P sw,69 Hold 2 P sw,70 S.Variation,71 S.Timbre,72 S.Release,73 S.Attack,74 S.Brightness,75 S.Ctrl 6,76 S.Ctrl 7,77 S.Ctrl 8,78 S.Ctrl 9,79 S.Ctrl 10,80 GP B.1 sw,81 GP B.2 sw,82 GP B.3 sw,83 GP B.4 sw,84,85,86,87,88,89,90,91 Effects Lv,92 Trem Lv,93 Chorus Lv,94 Celeste Lv,95 Phaser Lv,96 Data B. Inc,97 Data B. Dec,98 NRP L,99 NRP M,100 RP L,101 RP M,102,103,104,105,106,107,108,109,110,111,112,113,114,115,116,117,118,119,120,121,122,123,124,125,126,127}>Controller Source

slider3:maxCtrlVal=1<0,2,0.01>Max Controlling Value
slider4:minCtrlVal=0<0,1,0.01>Min Controlling Value
slider5:tension=0<-1,1,0.01>Mapping Tension
slider6:scroll=1<1,4,0.01>Scrolling Speed

@init

amp = 1;

histbuf = 1000000;
histpos = 0;
histsize = srate;

@block

scount = 0;
msgcount = 0;
msgplay = 0;

while(midirecv(offset, msg1, src, val)) (
    event = msg1 & 0xF0;
    chan = msg1 & 0x0F;

    // Controller
    event == 0xB0 && (chan == channel || channel == -1) && src = source ? (
        (2*msgcount)[0] = offset;
        (2*msgcount)[1] = val / 127;
        msgcount += 1;
    );
);

@sample

msgplay < msgcount ? (
    (2*msgplay)[0] <= scount ? (
        amp = (2*msgplay)[1];
        msgplay += 1;
    ) : (
        amp += ((2*msgplay)[1] - amp) / ((2*msgplay)[0] - scount + 1);
    );
);

amp_ = amp;
amp_ = tension > 0 ? 1 - (1 - amp) ^ exp(2.3 * tension) : amp ^ exp(-2.3 * tension);
amp_ = minCtrlVal + amp_ * (maxCtrlVal - minCtrlVal);

histbuf[histpos] = spl0;
histbuf[histpos + histsize] = spl1;
histbuf[histpos + 2*histsize] = amp_;

play_state == 1 ? (
    spl0 *= amp_;
    spl1 *= amp_;
);
scount += 1;

histpos += 1;
histpos >= histsize ? histpos=0;

@gfx 400 100

top = 0;
left = 0;
right = gfx_w;
bottom = gfx_h;

gscale = gfx_w/histsize * scroll;
gbuf = histbuf;
gscale2 = gfx_h;
gp = 0;

loop(5,
    ga = 0;
    offs = histpos;

    // original signal
    gp < 2 ? (
        gfx_set(0.1, 0.3, 0.6, 0.5);
        loop(histsize,
            gv = abs(gbuf[offs]);
            offs += 1;
            offs >= histsize ? offs = 0;
            gx = left + ga*gscale;
            gy = gfx_h - gv*gscale2;
            (ga || (gx > gfx_w && gfx_x > gfx_w)) ? gfx_lineto(gx, gy, 1) : ( gfx_x = gx; gfx_y = gy; );
            ga += 1;
        );

    // pumped signal
    ) : gp < 4 ? (
        gfx_set(0.5, 0.5, 0.3, 0.5);
        loop(histsize,
            gv = abs((gbuf - 2*histsize)[offs]) * histbuf[offs + 2*histsize];
            offs += 1;
            offs >= histsize ? offs = 0;
            gx = left + ga*gscale;
            gy = gfx_h - gv*gscale2;
            (ga || (gx > gfx_w && gfx_x > gfx_w)) ? gfx_lineto(gx, gy, 1) : ( gfx_x = gx; gfx_y = gy; );
            ga += 1;
        );

    // amp history
    ) : (
        gfx_set(0.5, 0.7, 0.4, 0.6);
        loop(histsize,
            gv = abs((gbuf - 2*histsize)[offs]);
            offs += 1;
            offs >= histsize ? offs = 0;
            gx = left + ga*gscale;
            gy = gfx_h - gv*gscale2;
            (ga || (gx > gfx_w && gfx_x > gfx_w)) ? gfx_lineto(gx, gy, 1) : ( gfx_x = gx; gfx_y = gy; );
            ga += 1;
        );
    );

    gp += 1;
    gbuf += histsize;
);

