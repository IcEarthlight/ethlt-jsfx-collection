desc: MultiSpect
author: Earthlight
version: 1.0

slider1:lowFreq=200<10,22000,1>-Crossover 1 (Hz)
slider2:highFreq=3000<10,22000,1>-Crossover 2 (Hz)
slider3:band=0<0,3,1{All,Low Band,Mid Band,High Band}>-Band
slider4:solo=0<0,1,1>-Solo

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

options:gfx_hz=60 no_meter

@init
cDenorm = 10^-30;
ext_tail_size = -1;
ext_noinit = 1;

histbuf = 1000000;
histpos = 0;
histsize = 2560;
pixelSplNum = 5 * srate / (510 - 80);

scount = 0;
smax = 0;
slmax = 0;
smmax = 0;
shmax = 0;

function update_mouse_state()
  instance(cap, x, y, lx, ly, dx, dy, right_click, left_click, lleft, lright, left, right, click_time, double_click, control, lwheel, wheel)
  global(mouse_cap, mouse_x, mouse_y, mouse_wheel)
(
    lleft = left;
    lright = right;
    lx = x;
    ly = y;
    cap = mouse_cap;
    control = mouse_cap & 4;
    x = mouse_x;
    y = mouse_y;
  
    left = cap & 1 > 0;
    right = cap & 2 > 0;
    left_click = left && lleft == 0;
    right_click = right && lright == 0;
    dx = x - lx;
    dy = y - ly;
  
    wheel = mouse_wheel > lwheel ? 1 : mouse_wheel < lwheel ? -1 : 0;
    lwheel = mouse_wheel;
  
    left_click ? (
        time_precise() - click_time < .5 ? double_click = 1;
        click_time = time_precise();
    ) : (
        double_click = 0;
    );
);

function mouse_in_rect (x, y, w ,h) (
  mouse.x >= x && mouse.x <= x + w && mouse.y >= y && mouse.y <= y + h;
);

@slider
xHP = exp(-2.0*$pi*highFreq/srate);
a0HP = 1.0 - xHP;
b1HP = -xHP;

xLP = exp(-2.0*$pi*lowFreq/srate);
a0LP = 1.0 - xLP;
b1LP = -xLP;

@sample

s0 = spl0;
s1 = spl1;

tmpl0LP = a0LP*s0 - b1LP*tmpl0LP;
tmpr0LP = a0LP*s1 - b1LP*tmpr0LP;
s0 -= tmpl0LP;
s1 -= tmpr0LP;

spl0l = (tmpl1LP = a0LP*tmpl0LP - b1LP*tmpl1LP);
spl1l = (tmpr1LP = a0LP*tmpr0LP - b1LP*tmpr1LP);
s0 = -s0 + (tmpl2LP = a0LP*s0 - b1LP*tmpl2LP);
s1 = -s1 + (tmpr2LP = a0LP*s1 - b1LP*tmpr2LP);

s0 /= xLP;
s1 /= xLP;

tmpl0HP = a0HP*s0 - b1HP*tmpl0HP;
tmpr0HP = a0HP*s1 - b1HP*tmpr0HP;
s0 -= tmpl0HP;
s1 -= tmpr0HP;

spl0m = (tmpl1HP = a0HP*tmpl0HP - b1HP*tmpl1HP);
spl1m = (tmpr1HP = a0HP*tmpr0HP - b1HP*tmpr1HP);
spl0h = -s0 + (tmpl2HP = a0HP*s0 - b1HP*tmpl2HP);
spl1h = -s1 + (tmpr2HP = a0HP*s1 - b1HP*tmpr2HP);

spl0h /= xHP;
spl1h /= xHP;

scount += 1;
smax =  max(smax,  max(abs(spl0),  abs(spl1)));
slmax = max(slmax, max(abs(spl0l), abs(spl1l)));
smmax = max(smmax, max(abs(spl0m), abs(spl1m)));
shmax = max(shmax, max(abs(spl0h), abs(spl1h)));
scount >= pixelSplNum ? (
    scount -= pixelSplNum;
    histbuf[histpos] = smax;
    histbuf[histpos + histsize] = slmax;
    histbuf[histpos + 2*histsize] = smmax;
    histbuf[histpos + 3*histsize] = shmax;

    smax = 0; slmax = 0; smmax = 0; shmax = 0;
    histpos += 1;
    histpos >= histsize ? histpos = 0;
);

solo ? (
    band == 1 ? ( spl0 = spl0l; spl1 = spl1l; )
    : band == 2 ? ( spl0 = spl0m; spl1 = spl1m; )
    : band == 3 ? ( spl0 = spl0h; spl1 = spl1h; );
);

@gfx 510 180

pixelSplNum = max(2, 5 * srate / (gfx_w - 80));

gfx_set(0.15, 0.1843, 0.1922, 1);
gfx_x = 80; gfx_y = 0;
gfx_rectto(gfx_w, gfx_h);

gfx_set(0.4275, 0.4627, 0.4706, 1);
gfx_x = gfx_y = 0;
gfx_rectto(80, 100);

gfx_set(0.3059, 0.3412, 0.3608);
gfx_x = 0; gfx_y = 100;
gfx_rectto(80, gfx_h);

gfx_set(0.75, 0.3569, 0.3294, 1);
gfx_rect(0, 0, 7, 23);
gfx_set(0.75, 0.5765, 0.3294, 1);
gfx_rect(0, 25, 7, 23);
gfx_set(0.7216, 0.7647, 0.3294, 1);
gfx_rect(0, 50, 7, 23);
gfx_set(0.49, 0.7569, 0.3255, 1);
gfx_rect(0, 75, 7, 23);

gfx_set(0.3451, 0.3765, 0.3804, 1);
gfx_rect(0, 23, 80, 2);
gfx_rect(0, 48, 80, 2);
gfx_rect(0, 73, 80, 2);
gfx_rect(0, 98, 80, 2);
gfx_rect(78, 0, 2, 100);

mouse.update_mouse_state();
mouse.left_click ? (
    mouse_in_rect(0, 0, 80, 25) ? band = 1
    : mouse_in_rect(0, 25, 80, 25) ? band = 2
    : mouse_in_rect(0, 50, 80, 25) ? band = 3
    : mouse_in_rect(0, 75, 80, 25) ? band = 0
    : mouse_in_rect(6, 115, 68, 25) ? solo = !solo;
);

gfx_set(0.2501, 0.2784, 0.2824, 1);
gfx_rect(6, 115, 68, 25);
gfx_set(0.5804, 0.6314, 0.6039, 0.8);
gfx_x = 34; gfx_y = 124;
gfx_printf("SOLO");
gfx_circle(21, 127.5, 5, 1, 1);
solo ? gfx_set(0.9333, 0.5137, 0.3647, 1)
     : gfx_set(0.3647, 0.3922, 0.4039, 1);
gfx_circle(21, 127.5, 3, 1, 1);

text_margin_x = 8;
text_margin_y = 8;

band == 1 ?
(
    gfx_set(0.3725, 0.4157, 0.4353, 1);
    gfx_rect(7, 0, 73, 23);

    gfx_set(0.8431, 0.8471, 0.7961, 1);
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y;
    gfx_printf("LOW");

    gfx_set(0.2902, 0.3255, 0.3294, 1);
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y + 25;
    gfx_printf("MID");
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y + 50;
    gfx_printf("HIGH");
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y + 75;
    gfx_printf("MASTER");
) : band == 2 ?
(
    gfx_set(0.3674, 0.4078, 0.4275, 1);
    gfx_rect(7, 25, 73, 23);

    gfx_set(0.8431, 0.8471, 0.7961, 1);
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y + 25;
    gfx_printf("MID");

    gfx_set(0.2902, 0.3255, 0.3294, 1);
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y;
    gfx_printf("LOW");
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y + 50;
    gfx_printf("HIGH");
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y + 75;
    gfx_printf("MASTER");
) : band == 3 ?
(
    gfx_set(0.3569, 0.4, 0.4196, 1);
    gfx_rect(7, 50, 73, 23);

    gfx_set(0.8431, 0.8471, 0.7961, 1);
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y + 50;
    gfx_printf("HIGH");

    gfx_set(0.2902, 0.3255, 0.3294, 1);
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y;
    gfx_printf("LOW");
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y + 25;
    gfx_printf("MID");
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y + 75;
    gfx_printf("MASTER");
) : (
    gfx_set(0.3412, 0.3804, 0.4);
    gfx_rect(7, 75, 73, 23);

    gfx_set(0.8431, 0.8471, 0.7961, 1);
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y + 75;
    gfx_printf("MASTER");

    gfx_set(0.2902, 0.3255, 0.3294, 1);
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y;
    gfx_printf("LOW");
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y + 25;
    gfx_printf("MID");
    gfx_x = text_margin_x + 7;
    gfx_y = text_margin_y + 50;
    gfx_printf("HIGH");
);

function db_mapping(x) (
    x < 0 ? x = 1.6*x - 0.6 * (exp(x) - 1);
    2 ^ (x/12 - 1);
);

offs = histpos;
gbuf = histbuf + band * histsize;
ga = 0;
loop(gfx_w - 80,
    offs -= 1;
    offs < 0 ? offs = histsize - 1;
    gv = gbuf[offs]>0 ? 6 * log(gbuf[offs]) / log(2) : -999;

    gx = gfx_w - 1 - ga;
    gy = (1 - db_mapping(gv)) * gfx_h;
    ceil(gy) < gfx_h ? (
        gfx_set(0.6784, 0.7490, 0.6902, 0.9);
        gfx_line(gx, ceil(gy), gx, gfx_h);
    );

    gfx_set(0.6784, 0.7490, 0.6902, 0.9 * (ceil(gy) - gy));
    gfx_line(gx, ceil(gy)-1, gx, ceil(gy)-1);
    ga += 1;
);

function draw_db_axis(x, s, offset) (
    gfx_set(1, 1, 1, 0.5);
    gfx_x = gfx_w - 3; gfx_y = (1 - db_mapping(x)) * gfx_h;
    gfx_lineto(gfx_w, gfx_y);

    gfx_set(1, 1, 1, 0.2);
    gfx_x -= offset; gfx_y -= 3;
    gfx_printf(s);
);

gfx_set(0.4157, 0.7412, 1, 1);
gfx_line(80, gfx_h/2, gfx_w, gfx_h/2);

draw_db_axis(-18, "-18", 28);
draw_db_axis(-12, "-12", 28);
draw_db_axis(-9, "-9", 20);
draw_db_axis(-6, "-6", 20);
draw_db_axis(-3, "-3", 20);
draw_db_axis(3, "3", 12);
draw_db_axis(6, "6", 12);
draw_db_axis(9, "9", 12);
